#!/bin/csh

# A shell script to add suppressed zeros to fortran output
# Unless there is an optional second argument, 
# it copies the original file (argument $1) to $1~
# If there is a second argument, it copies the original to that.

alias add0 'sed '"'"'s/ \(-*\)\.\([0-9]\)/\10.\2/g'"'" 


# --- Pick off switches ---
while (`echo $1 | sed -e 's/\(.\).*/\1/' `  ==  "-")
  set arg1 = $1; shift
  if ($?verb) echo test.lm: parsing switch $arg1
  switch ($arg1)
    case "--quiet":
      set quiet
      breaksw
    default:
      echo unrecognized switch $arg1
      echo 'usage: add0 [--quiet] [filename] [saved-filename]'
      exit -1
  endsw

end

if ($#argv == 0) exit
if (! -r $1) exit
if (! -w $1) exit


set tmpfile = $1~
if ($#argv > 1) set tmpfile = $2

if ($?quiet == 0) echo "add0: adding zeros to file $1 ... original in $tmpfile"

cp $1 $tmpfile
cat $tmpfile | add0 > $1

