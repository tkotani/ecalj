program lmfham1 ! Read HamiltonianPMT and generates MTO-only Hamiltonian
  use m_HamPMT,only: ReadHamPMTInfo, HamPMTtoHamRsMTO, plat,npair,nlat,nqwgt,ldim,nkp,qplist,ib_table,alat
  !                                  HamPMTtoHamRsMTO do not change variables here. Only generate HamRsMTO file.
  use m_HamRsMTO,  only: hammr,ovlmr,ndimMTO, ReadHamRsMTO, npairmx,nspx,ib_tableM
  use m_readqplist,only: eferm,qplistsy,ndat,xdat, Readqplistsy !qplist.dat is generated by job_band
  use m_mpi,only: MPI__hx0fp0_rankdivider2Q, MPI__Qtask, MPI__Initialize, MPI__Finalize,MPI__root, &
       MPI__Broadcast, MPI__rank,MPI__size, MPI__consoleout,MPI__barrier
  use m_ftox
  use m_lgunit,only:stdo
  use m_zhev,only:zhev_tk4
  implicit none
  integer:: i,j,ikp,ib1,ib2,it,nmx,nev,jsp
  complex(8)::img=(0d0,1d0),phase 
  real(8),allocatable:: evl(:)
  real(8)::qp(3),pi=4d0*atan(1d0),epsovl=0d0 !1d-8
  logical:: lprint=.true.,savez=.false.,getz=.false. 
  integer:: ndatx,ifsy1,ifsy2,ifsy,iix(36)
  logical:: symlcase=.true.
  character(256):: fband(2)=['band_lmfham1_spin1.dat','band_lmfham1_spin2.dat']
  call MPI__Initialize()
  call ReadHamPMTInfo()   ! Read infomation for PMT Hamiltonian (lattice structures and index of basis).
  call HamPMTtoHamRsMTO() ! HamRsMTO (real-space Hamiltonian hammr,ovlmr,ndimMTO) is generated,and written to a file HamRsMTO
  call ReadHamRsMTO()     ! Read real-space Hamiltonian hammr,ovlmr from HamRsMTO.
  if(symlcase) then ! When symlcase=T, read qplist.dat (q points list, see bndfp.F). 
     call readqplistsy()
     open(newunit=ifsy1,file=trim(fband(1)))
     if(nspx==2) open(newunit=ifsy2,file=trim(fband(2)))
     write(stdo,ftox)'Read qplist.dat: ndat =',ndat
  endif
  GetEigenvaluesForSYML: block! Get Hamitonian at k points from hammr,ovlmr (Realspace Hamiltonian), then diagnalize.
    ! bands by original ecalj (by job_band), and TB hamiltonian read by ReadHamiltonianPMTInfo.
    nmx = ndimMTO
    ndatx = nkp
    if(symlcase) ndatx=ndat
    do ikp=1,ndatx
       if(symlcase) then
          qp= qplistsy(:,ikp)
       else
          qp= qplist(:,ikp) 
       endif
       write(stdo,"(' ikp along qplist, q=',i5,3f9.4)")ikp,qp! true q(i)= 2pi/alat * qplist(i,ikp)
       Spinloop: do jsp=1,nspx !nsp is the number of spin.  When lso=1(Lz.Sz), nspx=1
          Hamblock: block
            complex(8):: ovlm(1:ndimMTO,1:ndimMTO),hamm(1:ndimMTO,1:ndimMTO),t_zv(ndimMTO,ndimMTO)
            real(8):: evl(ndimMTO)
            ovlm = 0d0
            hamm = 0d0
            FFTloop:do i=1,ndimMTO
               do      j=1,ndimMTO
                  ib1 = ib_tableM(i) !atomic-site index in the primitive cell
                  ib2 = ib_tableM(j)
                  do it =1,npair(ib1,ib2)
                     phase=1d0/nqwgt(it,ib1,ib2)*exp(-img*2d0*pi* sum(qp*matmul(plat,nlat(:,it,ib1,ib2))))
                     hamm(i,j)= hamm(i,j)+ hammr(i,j,it,jsp)*phase !Hamiltonian at qp
                     ovlm(i,j)= ovlm(i,j)+ ovlmr(i,j,it,jsp)*phase
                  enddo
               enddo
            enddo FFTloop
            call zhev_tk4(ndimMTO,hamm,ovlm,nmx,nev, evl,t_zv, epsovl)!Diangonalize (hamm- evl ovlm) z=0
            if(symlcase) then
               if(jsp==1) ifsy=ifsy1
               if(jsp==2) ifsy=ifsy2
               do i=1,nev
                  write(ifsy,"(f15.5,f15.5,2i4)") xdat(ikp),evl(i),jsp,i !for gnuplot
               enddo
            endif
          endblock Hamblock
       enddo Spinloop
    enddo
    close(ifsy1)
    if(nspx==2) close(ifsy2)
    Writebandplotlmfham1glt: block
      integer:: ifglt1,ifglt
      character(256):: aline,fname,fname1
      do jsp = 1,nspx
         fname ='bandplot.isp'//char(48+jsp)//'.glt'
         fname1='bandplot.lmfham1.isp'//char(48+jsp)//'.glt'
         open(newunit=ifglt1, file=trim(fname1))
         open(newunit=ifglt,  file=trim(fname))
         do 
            read(ifglt,"(a)",err=989,end=989)aline
            if(trim(aline)=="plot \") then !"
             write(ifglt1,ftox)"ef=",ftof(eferm)  
             write(ifglt1,ftox)trim(aline)
             write(ifglt1,ftox)'"'//trim(fband(jsp))//'" u ($1):(13.605*($2-ef)) pt 2 lc rgb "brown",\' !' 
            else
             write(ifglt1,ftox)trim(aline)
            endif   
         enddo
989      continue
         close(ifglt1)
         close(ifglt)
         write(stdo,ftox)'OK! Run gnuplot -p '//trim(fname1)//'. Brown points are by HamRsMTO for Hamiltonian on {|MLO1>}'
      enddo
    endblock Writebandplotlmfham1glt
    write(stdo,ftox)'Dim of |MLO1>based Hamiltonian HamRsMTO=',ndimMTO,'Atom-pair in real space=',npairmx
  endblock GetEigenvaluesForSYML
endprogram
