module m_lmfham1
  public lmfham1
  private
contains
!> PMT --1ststep--> MPO --2ndstep--> MLO. This is for 1ststep. called at the end of job_ham
subroutine lmfham1() ! Get the Hamiltoniand on the MT-Projected orbitals <MPO|H|MPO> and overlap <MPO|MPO>
  use m_HamPMT,only: ReadHamPMTInfo, HamPMTtoHamRsMPO, plat,npair,nlat,nqwgt,ldim,nkp,qplist,ib_table,alat,symops,ngrp,pos
  !                                  HamPMTtoHamRsMPO do not change variables here. Only generate HamRsMTO file.
  use m_HamRsMPO,  only: ReadHamRsMPO, hammr,ovlmr,ndimMTO, npairmx,nspx,ib_tableM
  use m_readqplist,only: Readqplistsy, eferm,qplistsy,ndat,xdat !qplist.dat is generated by job_band
  use m_ftox
  use m_lgunit,only: stdo,m_lgunit_init
  use m_zhev,only:   Zhev_tk4
  use m_MPItk,only: m_MPItk_init, nsize, procid,master_mpi,comm !  use m_ext,only:      m_ext_init,sname
  use m_keyvalue,only: Getkeyvalue
  use m_lmfinit,only:  m_lmfinit_init,oveps,nbas
  use m_ext,only: m_ext_init
  use m_cmdpath,only: Setcmdpath
  use m_prgnam,only: set_prgnam
  use m_setqibz_lmfham,only: set_qibz,qibz,nqibz
  use m_lattic,only:   m_lattic_init,  qlat=>lat_qlat
  use m_mksym,only:    m_mksym_init,iclasst
  use m_mkqp,only:     m_mkqp_init
  implicit none
  integer:: i,j,ikp,ib1,ib2,it,nmx,nev,jsp,ikpd,ierr
  complex(8)::img=(0d0,1d0),phase,aaaa
  real(8),allocatable:: evl(:)
  real(8)::qp(3),pi=4d0*atan(1d0)
  real(8)::facw,ecutw,eww,rydberg,posd(3)
  logical:: lprint=.true.,savez=.false.,getz=.false.
  integer:: ndatx,ifsy1,ifsy2,ifsy,iix(36),nsc1,iqini,iqend,ndiv
  logical:: symlcase=.true.
  character(256):: fband(2)=['band_MPO_spin1.dat','band_MPO_spin2.dat'],cccx
  character(8):: prgnam=''
  include "mpif.h"
  call setcmdpath() !Set self-command path (this is for call system at m_lmfinit)
  call m_ext_init()         ! Get sname, e.g. trim(sname)=si of ctrl.si
!  call mpi_init(ierr)
  call set_prgnam('lmfham1') ! mpi initialization
  call m_MPItk_init() ! mpi initialization
  call m_lgunit_init() !set stdo,stdl
  call m_lmfinit_init('lmfham1')! Read ctrlp into module m_lmfinit.
  call m_lattic_init()      ! lattice setup (for ewald sum)
  call m_mksym_init('LMF')  !symmetry go into m_lattic and m_mksym
  call m_mkqp_init() ! data of BZ go into m_mkqp
    
  call ReadHamPMTInfo()  ! Read info from PMTHamiltonianInfo (lattice structures and index of basis).
  call getkeyvalue("GWinput","mlo_facw",facw,default=.5d0)
  call getkeyvalue("GWinput","mlo_ecutw",ecutw,default=999*rydberg())
  call getkeyvalue("GWinput","mlo_eww",eww,default=.1d0*rydberg()) !size of fixing inner window
  if(master_mpi) write(stdo,ftox)'mlo_facw _ecutw (eV)=',ftof(facw),ftof(ecutw/rydberg())!,ftof(eww)
  ecutw= ecutw/rydberg()
  eww  = eww  /rydberg()
  if(symlcase) call readqplistsy()      ! When symlcase=T, read qplist.dat (q points list, see bndfp.F).
  
  call set_qibz(plat,qplist,nkp,symops,ngrp) ! Setup m_setqibz_lmfham
!  write(stdo,ftox)'nkp=',nkp
!  do i=1,nqibz
!     write(stdo,ftox) ftof(qibz(:,i))
!  enddo
!  call rx0('xxxxxxx111')
  
! !!! delta fun check for FFT: k --> T --> k ! \delta_{kk'} = \sum_{T \in T(i,j)} W_T exp( i (k-k') T)
!   do ikpd=1,nkp
!      write(stdo,*)'test for ikpd=',ikpd
!      do ikp=1,nkp
!         qp = qplist(:,ikp) - qplist(:,ikpd)
!         do ib1=1,nbas
!            do ib2=1,nbas
!               aaaa=0d0
!               do it = 1,npair(ib1,ib2)
!                  aaaa=aaaa+ 1d0/(nkp*nqwgt(it,ib1,ib2))*exp(img*2d0*pi* sum(qp*matmul(plat,nlat(:,it,ib1,ib2))))
!               enddo
!               cccx=''
!               if(ikp==ikpd) cccx=' <--'
!               if(abs(aaaa)>1d-8) then
!                  if(abs(aaaa-1d0)>1d-8) then
!                     write(stdo,ftox)'\delta-fun test ikpd ikp',ikpd,ikp,ftof(qplist(:,ikp)),ib1,ib2,ftof(aaaa),trim(cccx)
!                  endif   
!               endif
!            enddo
!         enddo
!      enddo
!   enddo
!   stop 'xxxxxxxxxxxxxxxxxxxxxx'
  
  call HamPMTtoHamRsMPO(facw,ecutw,eww) ! MT-projected orbital(MPO) Hamiltoinan. HamRsMPO
  ! (real-space Hamiltonian hammr,ovlmr,ndimMTO) is generated,and written to a file HamRsMPO
  call mpi_barrier(comm,ierr)
  call ReadHamRsMPO()                   ! Read real-space Hamiltonian hammr,ovlmr from HamRsMPO.
  if(symlcase) then
     if(master_mpi) open(newunit=ifsy1,file=trim(fband(1)))
     if(master_mpi.and.nspx==2) open(newunit=ifsy2,file=trim(fband(2)))
     if(master_mpi) write(stdo,ftox)'Read qplist.dat: ndat =',ndat
  endif
  nmx = ndimMTO
  ndatx = nkp
  if(symlcase) ndatx=ndat
  GetEigenvaluesForSYML: block!Get Hamitonian at k points from hammr,ovlmr (Realspace Hamiltonian), then diagnalize.
    real(8):: evl(ndimMTO,ndatx,nspx)
    integer:: ierr,ifixx
    evl=0d0
    ! bands by original ecalj is given by job_band. Read from ReadHamiltonianPMTInfo.
    ndiv= ndatx/nsize !!MPI division
    if(ndatx>ndiv*nsize) ndiv=ndiv+1
    iqini =     ndiv*procid+1
    iqend =     ndiv*procid+ndiv
    if(procid==nsize-1) iqend = min(ndatx,iqend)
    write(stdo,ftox)'nsize procid iqini iqend=',nsize,procid,iqini,iqend
    call mpi_barrier(comm,ierr)
    GetHamiltonianFromRealSpacehammrANDdiagonalize: do ikp=iqini,iqend !1,ndatx
       if(symlcase) then
          qp= qplistsy(:,ikp)
       else
          qp= qplist(:,ikp)
       endif !write(stdo,"(' procid ikp along qplist, q=',2i5,3f9.4)")procid,ikp,qp! true q(i)= 2pi/alat * qplist(i,ikp)
       Hamblock: block
         complex(8):: ovlm(1:ndimMTO,1:ndimMTO),hamm(1:ndimMTO,1:ndimMTO),t_zv(ndimMTO,ndimMTO)
         real(8):: rydberg
         Spinloop: do jsp=1,nspx !nsp is the number of spin.  When lso=1(Lz.Sz), nspx=1
            ovlm = 0d0
            hamm = 0d0
            FourierTransormationFROMrealspcaeTOqspace: do i=1,ndimMTO !MPO Hamiltonian
               do      j=1,ndimMTO
                  ib1 = ib_tableM(i) !atomic-site index in the primitive cell
                  ib2 = ib_tableM(j)
                  do it =1,npair(ib1,ib2)
                     phase=1d0/dble(nqwgt(it,ib1,ib2))*exp(-img*2d0*pi* sum(qp*matmul(plat,nlat(:,it,ib1,ib2))))
                     hamm(i,j)= hamm(i,j)+ hammr(i,j,it,jsp)*phase !MPO Hamiltonian at qp
                     ovlm(i,j)= ovlm(i,j)+ ovlmr(i,j,it,jsp)*phase
                  enddo
               enddo
            enddo FourierTransormationFROMrealspcaeTOqspace
            call zhev_tk4(ndimMTO,hamm,ovlm,0,nev, evl(:,ikp,jsp),t_zv, oveps)!nmx=0 means only eigenvalue. Diangonalize (hamm- evl ovlm) z=0
            !write(stdo,ftox) '  evl   =',nev,ftof(evl(1:10,ikp,jsp)*rydberg())
         enddo Spinloop
         !write(stdo,"(' xxx procid ikp along qplist, q=',2i5,3f9.4)")procid,ikp,qp! true q(i)= 2pi/alat * qplist(i,ikp)
       endblock Hamblock
    enddo GetHamiltonianFromRealSpacehammrANDdiagonalize
    call mpi_barrier(comm,ierr)
    call mpibc2_real(evl,size(evl),'lmfham1_evl')
    if(symlcase.and.master_mpi) then
       do ikp=1,ndatx
          do jsp=1,nspx
             if(jsp==1) ifsy=ifsy1
             if(jsp==2) ifsy=ifsy2
             do i=1,nev
                write(ifsy,"(f15.5,f15.5,2i4)") xdat(ikp),evl(i,ikp,jsp),jsp,i !for gnuplot
             enddo
          enddo
       enddo
    endif
    if(master_mpi) close(ifsy1)
    if(master_mpi.and.nspx==2) close(ifsy2)
    Writebandplotlmfham1glt: block
      integer:: ifglt1,ifglt
      character(256):: aline,fname,fname1
      do jsp = 1,nspx
         fname ='bandplot.isp'//char(48+jsp)//'.glt'
         fname1='bandplot_MPO.isp'//char(48+jsp)//'.glt'
         open(newunit=ifglt1, file=trim(fname1))
         open(newunit=ifglt,  file=trim(fname))
         do
          read(ifglt,"(a)",err=989,end=989)aline
          if(aline(1:4)=='plot') then 
            write(ifglt1,ftox)"ef=",ftof(eferm)
            write(ifglt1,ftox)trim(aline)
            write(ifglt1,ftox)'"'//trim(fband(jsp))//'" u ($1):(13.605*($2-ef)) pt 2 lc rgb "green",\' !'
          else
            write(ifglt1,ftox)trim(aline)
          endif
        enddo
989     continue
        close(ifglt1)
        close(ifglt)
        if(master_mpi) write(stdo,ftox)&
          'OK! Run gnuplot -p '//trim(fname1)//'. Brown points are by HamRsMTO for Hamiltonian on {|MLO1>}'
      enddo
    endblock Writebandplotlmfham1glt
    if(master_mpi) write(stdo,ftox)'Dim of |MLO1>based Hamiltonian HamRsMTO=',ndimMTO,'Atom-pair in real space=',npairmx
  endblock GetEigenvaluesForSYML
  call rx0('OK! end of lmfham1. Get MPO Hamiltoian')
end subroutine lmfham1
end module m_lmfham1
